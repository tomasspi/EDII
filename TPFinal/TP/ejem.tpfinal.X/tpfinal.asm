#include "p16f887.inc"

; CONFIG1
; __config 0x3FF9
 __CONFIG _CONFIG1, _FOSC_XT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF
; CONFIG2
; __config 0x3FFF
 __CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF

 CBLOCK 0x70
 DECENA
 UNIDAD
 GRADOS
 CELSIUS
 WA
 STATUSA
 CONTADOR0
 CONTADOR1
 MUX
 DLAY
 NUM_DEC
 NUM_UNI
 ENDC
 
	  ORG 0x00
	  GOTO INICIO
	  ORG 0x04
	  GOTO IRS

 INICIO
	  ;----- CONFIGURACIÓN DE PUERTOS -----
          BANKSEL TRISA
	  CLRF TRISD        ;PUERTO D COMO SALIDA
	  MOVLW B'00000000' ;RC6 COMO SALIDA PARA Tx, EL RESTO SALIDAS
	  MOVWF TRISC       
	  MOVLW B'00000001'
	  MOVWF TRISB ; RB0 COMO ENTRADA
	  MOVWF	TRISA ; RA0 COMO ENTRADA
	  
	  BANKSEL ANSEL ;RA0 COMO ENTRADA ANALÓGICA
	  MOVLW 0x01	;EL RESTO DE LAS E/S COMO DIGITALES
	  MOVWF ANSEL
	  CLRF ANSELH
	  
	  ;----- CONFIGURACIÓN PARA TRANSMISIÓN POR PUERTO SERIE -----
	  BANKSEL TXSTA
	  BCF TXSTA, SYNC
          BANKSEL RCSTA
	  BSF RCSTA, SPEN      
      
	  BANKSEL TXSTA
	  MOVLW   b'00100100'   
	  MOVWF   TXSTA      ;Tx = ON, ASÍNCRONO CON 8 BITS Y ALTA VELOCIDAD
      
	  BANKSEL SPBRG
	  MOVLW   .25
	  MOVWF   SPBRG      ;9600 BAUDIOS CON Fosc=4MHz
	  ;-----------------------------------------------------------
	  
	  ;----- PRE-SCALER EN 256 Y ACTIVACIÓN DEL PULL-UPS -----
	  BANKSEL OPTION_REG
	  MOVLW B'00000111'
	  MOVWF OPTION_REG       
	  MOVLW B'00000001'
	  MOVWF	WPUB		 
	  
	  ;----- TIMER 1 COMO TEMPORIZADOR CON PRE-SCALER EN 8 -----
   
      
	  ;----- CONFIGURACIÓN DEL ADC  -----
	  ;----- RESULTADO JUSTIFICADO A LA IZQUIERDA -----
	  ;----- Vref+ = Vcc (5V), Vref- = GND(0V) -----
	  BANKSEL ADCON1
	  CLRF ADCON1 
      
	  ;----- Fosc/32, CANAL ANALÓGICO 0 (AN0), ADC HABILITADO -----
	  BANKSEL ADCON0
	  MOVLW B'10000001'      
	  MOVWF ADCON0    
      
	  ;----- ACTIVACIÓN DE INTERRUPCIONES POR TIMER 0 Y RB0 -----
	  BANKSEL INTCON
	  MOVLW B'01110000'
	  MOVWF INTCON   
	  
	  ;----- CARGA DEL TIMER 0 -----
	  BANKSEL TMR0
	  MOVLW .236      
	  MOVWF TMR0     
	  
	  ;----- LIMPIA LOS PUERTOS -----
	  CLRF PORTD 
	  CLRF PORTC       
	  CLRF PORTB
	  CLRF PORTA
	  
	  ;----- CARGA LOS VALORES POR DEFECTO A MOSTRAR EN LOS DISPLAYS: 00°C -----
	  MOVLW 0x3F
	  MOVWF UNIDAD       
	  MOVLW 0x3F
	  MOVWF DECENA     
	  MOVLW 0x63		    
	  MOVWF GRADOS
	  MOVLW 0x39
	  MOVWF CELSIUS
	  MOVLW .4
	  MOVWF CONTADOR0 
	  MOVLW .10
	  MOVWF CONTADOR1
	  BANKSEL PORTA
	  BSF	  INTCON,GIE
	  
	  GOTO $
	  
IRS	  ;----- SALVAR CONTEXTO -----
	  MOVWF WA
	  SWAPF STATUS,W
	  MOVWF STATUSA
	  
	  ;----- VER BANDERAS -----
	  BTFSC INTCON,INTF    
	  GOTO RB0SR
	  BTFSC INTCON,T0IF   
	  GOTO TMR0SR
	  BTFSC PIR1,TMR1IF
	  GOTO TMR1SR
	  BTFSC PIR1,ADIF
	  GOTO ADSR
	  GOTO SALIR
	  
;----- INTERRUPCIÓN POR RB0 -----
RB0SR	  BCF INTCON,INTF
	  BSF ADCON0,1
	  BANKSEL T1CON
	  MOVLW B'00110001'
	  MOVWF T1CON 
	  BANKSEL TMR1L
	  MOVLW 0X0B
	  MOVWF TMR1H     
	  MOVLW 0xDC
	  MOVWF TMR1L
	  BANKSEL PIE1
	  BSF PIE1,ADIE
	  BSF PIE1,TMR1IE
	  BANKSEL PORTA
	  GOTO SALIR

;----- INTERRUPCIÓN POR CONVERSOR -----
;----- MODIFICA EL VALOR A MOSTRAR -----
ADSR	  BCF PIR1,ADIF
	  BCF STATUS,C
	  BANKSEL ADRESL
	  RLF ADRESL,F
	  BANKSEL ADRESH
	  RLF ADRESH,F
	  MOVF ADRESH,W
	  CALL OBTENER_DECENA
	  CALL OBTENER_UNIDAD
	  GOTO SALIR

;----- OBTENER DECENA -----
OBTENER_DECENA
	  CLRF	NUM_DEC
	  MOVLW	.10
RESTAR	  SUBWF	ADRESH,F
	  INCF	NUM_DEC
	  BTFSC	STATUS,C
	  GOTO	RESTAR
	  DECF	NUM_DEC,F
	  MOVLW	.10
	  ADDWF	ADRESH,F
	  MOVF	NUM_DEC,W
	  CALL	TABLA
          MOVWF	DECENA
	  RETURN    

;----- OBTENER UNIDAD -----
OBTENER_UNIDAD	
	  CLRF	NUM_UNI
	  MOVLW	.1
RESTARU	  SUBWF	ADRESH,F
	  INCF	NUM_UNI
	  BTFSC	STATUS,C
	  GOTO	RESTARU
	  DECF	NUM_UNI,F
	  MOVLW	.1
	  ADDWF	ADRESH,F
	  MOVF	NUM_UNI,W
	  CALL	TABLA
          MOVWF	UNIDAD
	  RETURN

;----- INTERRUPCIÓN POR TIMER0 -----
TMR0SR	  BCF INTCON,T0IF
	  MOVLW .236
	  MOVWF	TMR0
	  DECFSZ CONTADOR0,F
	  GOTO MOSTRAR	  
	  GOTO MOVDECENA	
	      
;----- MUESTRA LOS VALORES EN LOS CONTADOR0S -----
MOSTRAR	 
	  BCF STATUS,Z
	  MOVF CONTADOR0,W	    
	  SUBLW	.3
	  MOVWF	MUX
	  BTFSC	STATUS,Z
	  GOTO MOVLETRA
	  BTFSC	MUX,0
	  GOTO MOVGRADO
	  BTFSC	MUX,1
	  GOTO MOVUNIDAD
	  
MOVLETRA  
	  MOVF CELSIUS,W
	  MOVWF	PORTD
	  CLRF PORTC
	  BSF PORTC,3
	  GOTO SALIR
MOVGRADO  
	  MOVF GRADOS,W
	  MOVWF	PORTD
	  CLRF PORTC
	  BSF PORTC,2	 
	  GOTO SALIR
MOVUNIDAD 
	  MOVF UNIDAD,W
	  MOVWF	PORTD
	  CLRF PORTC
	  BSF PORTC,1	 
	  GOTO SALIR
MOVDECENA 
	  MOVF DECENA,W
	  MOVWF PORTD
	  CLRF PORTC
	  BSF PORTC,0
	  MOVLW .4
	  MOVWF	CONTADOR0
	  GOTO SALIR	

;----- INTERRUPCIÓN POR TIMER1 -----
TMR1SR	  BCF PIR1,TMR1IF
	  MOVLW	0x0B
	  MOVWF	TMR1H
	  MOVLW	0xDC
	  MOVWF	TMR1L
	  DECFSZ CONTADOR1
	  GOTO SALIR
	  MOVLW .10
	  MOVWF CONTADOR1
	  BSF ADCON0,GO 
	  CALL ENVIAR
	  
;----- RECUPERAR CONTEXTO -----	  
SALIR
	  SWAPF	STATUSA,W
	  MOVWF	STATUS
	  SWAPF	WA,F
	  SWAPF	WA,W
	  RETFIE	  

;----- DELAY PARA ADQUISICION DEL DATO -----
DELAY	  MOVLW .250
	  MOVWF DLAY
	  DECFSZ DLAY		;250[uS]
	  GOTO $-1
	  RETURN
	  
;----- TABLAS CON LOS VALORES DEL 0 AL 9 PARA LOS DISPLAYS -----
TABLA     ADDWF PCL,F
	  RETLW 0X3F       ;0
          RETLW 0x06       ;1
	  RETLW 0x5B       ;2
	  RETLW 0x4F       ;3
	  RETLW 0x66       ;4
	  RETLW 0x6D       ;5
	  RETLW 0x7D       ;6
	  RETLW 0x07       ;7
	  RETLW 0x7F       ;8
	  RETLW 0x6F       ;9

ENVIAR
	  MOVLW	0x30
	  ADDWF	NUM_DEC,W
	  MOVWF	TXREG
	  CALL	DELAY
	  CALL	DELAY
	  MOVLW	0x30
	  ADDWF	NUM_UNI,W
	  MOVWF	TXREG
	  CALL	DELAY
	  MOVLW	0xA7
	  MOVWF	TXREG
	  CALL DELAY
	  MOVLW	'C'
	  MOVWF	TXREG
	  CALL DELAY
	  MOVLW	0x09
	  MOVWF	TXREG
	  CALL	DELAY
	  RETURN
	  
	  END